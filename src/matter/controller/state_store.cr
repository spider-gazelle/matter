require "../storage/backend"
require "./state"

module Matter
  module Controller
    # Persists the controller `State` as the `controller/state` document of a
    # `Storage::Backend`.
    class StateStore
      COLLECTION  = "controller"
      DOCUMENT_ID = "state"

      getter backend : Storage::Backend

      def initialize(@backend : Storage::Backend)
      end

      def load : State
        @backend.open unless @backend.open?

        document = @backend.read(COLLECTION, DOCUMENT_ID)
        state = document ? State.from_document(document) : State.new

        state, dirty = normalize(state)
        save(state) if dirty && document
        state
      rescue ex : Matter::StorageError
        raise ex
      rescue ex
        raise Matter::StorageError.new("Failed to load controller state (path=#{@backend.path.inspect}): #{ex.message}", cause: ex)
      end

      def save(state : State) : Nil
        @backend.open unless @backend.open?
        @backend.write(COLLECTION, DOCUMENT_ID, state.to_document)
      rescue ex : Matter::StorageError
        raise ex
      rescue ex
        raise Matter::StorageError.new("Failed to save controller state (path=#{@backend.path.inspect}): #{ex.message}", cause: ex)
      end

      private def normalize(state : State) : {State, Bool}
        dirty = false

        if fabric = state.fabric
          if state.commissioner_node_id != fabric.controller_node_id
            state = State.new(
              fabric: state.fabric,
              nodes: state.nodes,
              commissioner_node_id: fabric.controller_node_id,
              unsecured_message_counter: state.unsecured_message_counter
            )
            dirty = true
          end
        elsif state.commissioner_node_id == 0_u64
          state = State.new(
            fabric: state.fabric,
            nodes: state.nodes,
            commissioner_node_id: random_nonzero_u64,
            unsecured_message_counter: state.unsecured_message_counter
          )
          dirty = true
        end

        {state, dirty}
      end

      private def random_nonzero_u64 : UInt64
        loop do
          id = Random::Secure.rand(UInt64)
          return id unless id == 0_u64
        end
      end
    end
  end
end
